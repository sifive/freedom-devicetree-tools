tuple DTCOptions =
  global InFormat:    DTCFormat
  global InputFile:   Path
  global OtherInputs: List Path
  global OutFormat:   DTCFormat
  global OutputFile:  String

global data DTCFormat =
  DTC_DTB
  DTC_DTS
  DTC_FS

global def dtcFormatToString = match _
  DTC_DTB = "dtb"
  DTC_DTS = "dts"
  DTC_FS  = "fs"

global def makeDTCOptions inFormat outFormat inFile otherInputs outputFile =
  DTCOptions inFormat inFile otherInputs outFormat outputFile

global def dtsTodtb = makeDTCOptions DTC_DTS DTC_DTB _ _ _ | runDTC
global def dtbTodts = makeDTCOptions DTC_DTB DTC_DTS _ _ _ | runDTC

global def runDTC options =
  def cmdline =
    which "dtc",
    options.getDTCOptionsInputFile.getPathName,
    "-o", options.getDTCOptionsOutputFile,
    "-I", options.getDTCOptionsInFormat.dtcFormatToString,
    "-O", options.getDTCOptionsOutFormat.dtcFormatToString,
    Nil
  def outputDir = mkdir "{options.getDTCOptionsOutputFile}/.."
  def inputs = outputDir, options.getDTCOptionsInputFile, options.getDTCOptionsOtherInputs
  makePlan cmdline inputs
  | runJob
  | getJobOutput
